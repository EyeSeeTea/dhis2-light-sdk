plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.71'
}
apply plugin: 'java'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    def okHttpVersion = "3.11.0"
    def retrofitVersion = "2.3.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    ktlint 'com.github.shyiko:ktlint:0.29.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation ("com.squareup.retrofit2:retrofit:$retrofitVersion"){
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }

    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.amshove.kluent:kluent:1.4'
    //compile "com.google.code.gson:gson:2.8.0"
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
